apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'


if (getGradle().getStartParameter().getTaskRequests().toString().toLowerCase().contains("gms")) {
    apply plugin: 'com.google.gms.google-services'
} else {
    apply plugin: 'com.huawei.agconnect'
}

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.2"
    defaultConfig {
        applicationId "com.dtsedemo"
        minSdkVersion 18
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    flavorDimensions "version"
    productFlavors{
        gms{
            dimension "version"
            applicationIdSuffix ".gms"
            resValue "string", "app_name", "GmsDemo"

        }
        hms{
            dimension "version"
            applicationIdSuffix ".hms"
            resValue "string", "app_name", "HmsDemo"

        }
    }

    signingConfigs {
        release{
            storeFile file('*****.jks')
            keyAlias '*****'
            keyPassword '*******'
            storePassword '**********'
            v1SigningEnabled true
            v2SigningEnabled true
        }
    }

    buildTypes {

        debug{
            signingConfig signingConfigs.release
            debuggable true
        }

        release {
            signingConfig signingConfigs.release
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }


}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation"org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.core:core-ktx:1.1.0'

    gmsImplementation 'com.google.firebase:firebase-messaging:20.1.0'
    gmsImplementation 'com.google.android.gms:play-services-auth:17.0.0'

    hmsImplementation 'com.huawei.hms:push:4.0.0.300'
    hmsImplementation 'com.huawei.hms:hwid:4.0.0.300'

}
